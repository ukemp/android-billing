plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

def versionMajor = 7
def versionMinor = 1
def versionPatch = 1

android {
    compileSdk 35

    namespace = "de.kempmobil.billing"

    defaultConfig {
        minSdk 19
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    publishing {
        singleVariant("release") {
            withJavadocJar()
            withSourcesJar()
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // Important to add the billing library as a transitive dependency using 'api',
    // so it will be available in projects using this library:
    api "com.android.billingclient:billing-ktx:7.1.1"

    implementation 'androidx.core:core-ktx:1.15.0'
    implementation "com.jakewharton.timber:timber:5.0.1"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'de.kempmobil.android'
                artifactId = 'billing'
                version = "${versionMajor}.${versionMinor}.${versionPatch}"
            }
        }
        repositories {
            maven {
                url "https://maven.pkg.github.com/ukemp/billing"
                name "Android Libraries"
                credentials {
                    username = "ukemp"
                    password = githubDeployToken
                }
            }
        }
    }
}